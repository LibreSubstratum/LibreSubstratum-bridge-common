buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def tag = System.getenv("TRAVIS_TAG") ?: "a"
//Remove v from vVERSIONNUMBER
def libraryVersion = tag.substring(1)

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: "b"
    key = System.getenv('BINTRAY_KEY') ?: "c"
    publish = true
    pkg {
        repo = 'maven'
        name = "LibreSubstratumService"
        publish = true
        publicDownloadNumbers = true
        version {
            name = "$libraryVersion"
            vcsTag = "$tag"
        }
    }
    publications = ['MyPublication']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'com.libresubstratum'
            artifactId 'service'
            version libraryVersion
            artifact "${project.buildDir}/outputs/aar/${project.getName()}-release.aar"
            artifact sourcesJar
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}